apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'application'

def defaultEncoding = 'UTF-8'
def jdkVersion = '1.8'

archivesBaseName = 'it-events'

mainClassName = 'tsukurukai.it_events.ItEventsApplication'

project.ext {
    dropWizardConfig = './conf/it-events.yml'
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'io.dropwizard:dropwizard-core:0.7.0'
    compile 'org.projectlombok:lombok:1.14.2'
    compile 'org.apache.httpcomponents:httpclient:4.3.4'
    testCompile 'org.codehaus.groovy:groovy-all:2.0.8'
    testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
}

compileJava {
    options.encoding = defaultEncoding
}
compileTestJava {
    options.encoding = defaultEncoding
}

idea {
    project {
        jdkName = jdkVersion
        languageLevel = jdkVersion
    }
}

jar {
    from (configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude 'META-INF/MANIFEST.MF'
        exclude 'META-INF/*.SF'
        exclude 'META-INF/*.DSA'
        exclude 'META-INF/*.RSA'
    }
    manifest {
        attributes('Main-Class': 'tsukurukai.it_events.ItEventsApplication')
    }
}

run {
    args 'server', dropWizardConfig
}

task stage(dependsOn: 'build') {
    mustRunAfter 'clean'
}

